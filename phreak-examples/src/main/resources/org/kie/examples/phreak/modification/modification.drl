package org.kie.examples.phreak.modification

import org.kie.examples.phreak.domain.Account
import org.kie.examples.phreak.domain.Account.Category
import org.kie.examples.phreak.domain.Transaction

dialect "mvel"

rule gold_account
salience 200
lock-on-active
when
  account: Account()
  Number(this >= 50000) from accumulate(t: Transaction(source == account); sum(t.amount))
  Number(this >= 50000) from accumulate(t: Transaction(target == account); sum(t.amount))
then
  modify (account) {
    setCategory(Category.GOLD)
  }
  //System.out.println("Gold account: " + account);
end

rule silver_account
salience 100
lock-on-active
when
  account: Account(category != Category.GOLD)
  Number(this >= 25000) from accumulate(t: Transaction(source == account); sum(t.amount))
  Number(this >= 25000) from accumulate(t: Transaction(target == account); sum(t.amount))
then
  modify (account) {
    setCategory(Category.SILVER)
  }
  //System.out.println("Silver account: " + account);
end

rule bronze_account
salience 50
lock-on-active
when
  account: Account(category != Category.SILVER && category != Category.GOLD)
  Number(this >= 10000) from accumulate(t: Transaction(source == account); sum(t.amount))
  Number(this >= 10000) from accumulate(t: Transaction(target == account); sum(t.amount))
then
  modify (account) {
    setCategory(Category.BRONZE)
  }
  //System.out.println("Bronze account: " + account);
end
